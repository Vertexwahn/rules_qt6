load("@rules_qt//:qt_libraries.bzl", "QT_LIBRARIES")
load("@rules_qt//:qt.bzl", "qt_cc_binary")

[
    cc_library(
        name = "qt_%s" % name,
        visibility = ["//visibility:public"],
        deps = dependencies + select({
            "@platforms//os:linux": ["@qt_6.2.4_linux_desktop_gcc_64//:qt_%s_linux" % name],
            "@bazel_tools//src/conditions:darwin_x86_64": ["@qt_6.2.4_mac_desktop_clang_64//:qt_%s_mac" % name],
            "@bazel_tools//src/conditions:darwin_arm64": ["@qt_6.4.0_mac_desktop_clang_64_M1//:qt_%s_mac" % name],
            "@platforms//os:windows": ["@qt_6.2.4_windows_desktop_win64_msvc2019_64//:qt_%s_windows" % name],
        }),
    )
    for name, _, _, dependencies in QT_LIBRARIES
]

cc_library(
    name = "qt_hdrs",
    visibility = ["//visibility:public"],
    deps = select({
        "@platforms//os:linux": ["@qt_6.2.4_linux_desktop_gcc_64//:qt_hdrs"],
        "@bazel_tools//src/conditions:darwin_x86_64": ["@qt_6.2.4_mac_desktop_clang_64//:qt_hdrs"],
        "@bazel_tools//src/conditions:darwin_arm64": ["@qt_6.4.0_mac_desktop_clang_64_M1//:qt_hdrs"],
        "@platforms//os:windows": ["@qt_6.2.4_windows_desktop_win64_msvc2019_64//:qt_hdrs"],
    }),
)

all_qt_libs = [":qt_%s" % name for name, _, _, _ in QT_LIBRARIES]

cc_library(
    name = "qt",
    visibility = ["//visibility:public"],
    deps = all_qt_libs + [":qt_hdrs"],
)

qt_cc_binary(
    name = "Qt6HelloWorld",
    srcs = ["main.cpp"],
    deps = [
        ":qt_core",
        ":qt_qml",
        ":qt_widgets",
    ],
)
